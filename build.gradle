

allprojects {
    apply plugin: 'java'

    repositories {
        jcenter()
        mavenCentral()
    }

    dependencies {
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

        // Test
        testImplementation 'org.mockito:mockito-core:2.+'
        testImplementation 'com.github.javafaker:javafaker:1.0.1'
    }
    test {
        useJUnitPlatform()

        testLogging {
            events "passed", "skipped", "failed"
        }
    }


    task view_paths {
        doLast { task ->
            println "$task.project.name"
            println "------------------"
            println "Main: $sourceSets.main.java.srcDirTrees"
            println "   Resources: $sourceSets.main.resources.srcDirTrees"
            println "Tests: $sourceSets.test.java.srcDirTrees"
            println "   Resources: $sourceSets.test.resources.srcDirTrees"
        }
    }
}

subprojects {
    group = "com.prueba.${rootProject.name}"

    sourceSets {
        main {
            java { srcDirs = ['main'] }
            resources { srcDirs = ['main/resources'] }
        }

        test {
            java { srcDirs = ['test'] }
            resources { srcDirs = ['test/resources'] }
        }
    }

    jar {
        enabled = true
    }
}

apply plugin: 'application'

dependencies {

    implementation project(":shared")
    implementation project(":administration")

    testImplementation project(":shared").sourceSets.test.output
}

